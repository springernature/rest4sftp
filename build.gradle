plugins {
//    id 'application'
    id 'org.jetbrains.kotlin.jvm' version "${kotlin_version}"
    id 'com.bmuschko.nexus' version '2.3.1'
}

group 'com.ubertob.rest4sftp'
version '1.0.4'



wrapper {
    gradleVersion = "6.8.3"
}

sourceCompatibility = "${jvmVersion}"
targetCompatibility = "${jvmVersion}"

repositories {
    mavenCentral()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "${jvmVersion}"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "${jvmVersion}"
    }
}


test {
    exclude 'integrationtest/**/*'

    useJUnitPlatform()
    testLogging.showStandardStreams = true
}

task integrationTest(type: Test) {
    include 'integrationtest/**/*'

    useJUnitPlatform()
    testLogging.showStandardStreams = true
}


//applicationName = archivesBaseName
//applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8", "-Duser.timezone=UTC"]

//mainClassName = "com.ubertob.rest4sftp.main.MainKt"


dependencies{

    implementation "org.http4k:http4k-core:${http4kVersion}"
    implementation "org.http4k:http4k-client-apache4:${http4kVersion}"
    implementation "org.http4k:http4k-server-jetty:${http4kVersion}"
    implementation "org.http4k:http4k-multipart:${http4kVersion}"
    implementation "com.ubertob.kondor:kondor-core:${kondorVersion}"
    implementation 'com.xenomachina:kotlin-argparser:2.0.7'
    implementation 'commons-net:commons-net:3.6'
    implementation 'com.hierynomus:sshj:0.27.0'

    implementation "org.slf4j:slf4j-simple:${slf4jVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"


    testImplementation   'com.willowtreeapps.assertk:assertk-jvm:0.13'
    testImplementation   "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation   "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation   "org.junit.jupiter:junit-jupiter-params:${junitVersion}"

}

archivesBaseName = 'rest4sftp'

modifyPom {
    project {
        name 'rest4sftp'
        description 'Bridge from RESTful api to SFTP and FTP'
        url 'https://github.com/uberto/rest4sftp'
        inceptionYear '2019'
        scm {
            url 'https://github.com/uberto/rest4sftp'
            connection 'https://github.com/uberto/rest4sftp.git'
            developerConnection 'git@github.com:uberto/rest4sftp.git'
        }
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'UbertoBarbini'
                name 'Uberto Barbini'
                email 'uberto.gama@gmail.com'
            }
            developer {
                id 'tompower-sn'
                name 'Tom Power'
                email 'tom.power@springernature.com'
            }
        }
    }
}
extraArchive {
    sources = true
    tests = false
    javadoc = true
}
nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}